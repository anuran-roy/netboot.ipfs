#!ipxe

# OpenBSD Operating System
# http://www.openbsd.org
set ipfs_root QmWpXdpAiidSKMTMjvumuXbYcsJpxRyeYVdKPZkLSsCR3M

:openbsd_menu
menu OpenBSD
item 6.0 OpenBSD 6.0
item 5.9 OpenBSD 5.9
item snapshots OpenBSD 6.0 Latest Snapshot
choose ver || goto openbsd_exit

iseq ${ver} 6.0 && set image_ver 60 ||
iseq ${ver} 5.9 && set image_ver 59 ||
iseq ${ver} snapshots && set image_ver 60 ||

iseq ${arch} x86_64 && goto openbsd_x64 ||
set openbsd_arch i386
goto boot_openbsd

:openbsd_x64
set openbsd_arch amd64
goto boot_openbsd

:boot_openbsd
set src ${ipfs_gateway}/${ipfs_root}/${ver}/${openbsd_arch}/cd${image_ver}.iso
imgfree
initrd ${src}
chain ${memdisk} iso raw
exit

:openbsd_exit
clear menu
exit 0
