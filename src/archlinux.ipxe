#!ipxe

# Arch Linux Operating System
# http://www.archlinux.org
set ipfs_root QmPC2yKg7gYkPcLT5D78U2z2YroKY17RbpRLn69cbviWMW

goto ${menu} ||

:archlinux
set os Arch Linux
clear arch_version
menu ${os} - ${arch} - Image Sig Checks: [${img_sigs_enabled}]
menu Arch Linux
item --gap Latest Releases
item 2016.11.03 2016.11.03
item 2016.12.01 2016.12.01
item 2017.01.01 2017.01.01
choose arch_version || goto archlinux_exit
goto boot

:boot
imgfree
iseq ${arch} i386 && set bootarch i686 || set bootarch x86_64
set dir ${arch_version}/arch/boot
set params initrd=archiso.img archiso_http_srv=${ipfs_gateway}/${ipfs_root}/${arch_version}/ archisobasedir=arch verify=y ip=dhcp net.ifnames=0
kernel ${ipfs_gateway}/${ipfs_root}/${dir}/${bootarch}/vmlinuz ${params}
initrd ${ipfs_gateway}/${ipfs_root}/${dir}/${bootarch}/archiso.img
echo
echo MD5sums:
md5sum vmlinuz archiso.img
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify vmlinuz ${sigs}${dir}/${bootarch}/vmlinuz.sig || goto error
imgverify archiso.img ${sigs}${dir}/${bootarch}/archiso.img.sig || goto error
echo Signatures verified!
echo
:skip_sigs
boot
goto archlinux_exit

:archlinux_exit
clear menu
exit 0
